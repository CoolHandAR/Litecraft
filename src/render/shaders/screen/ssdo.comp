#version 460 core

#include "../shader_commons.incl"
#include "../scene_incl.incl"

layout (local_size_x = 8, local_size_y = 8, local_size_z = 1) in;

layout(rgba16f, binding = 0) writeonly restrict uniform image2D ssaoOutput;

#define NUM_KERNLES_SAMPLES 32
const vec3 kernelSamples[NUM_KERNLES_SAMPLES] = 
{
		vec3(-0.134, 0.044, -0.825),
		vec3(0.045, -0.431, -0.529),
		vec3(-0.537, 0.195, -0.371),
		vec3(0.525, -0.397, 0.713),
		vec3(0.895, 0.302, 0.139),
		vec3(-0.613, -0.408, -0.141),
		vec3(0.307, 0.822, 0.169),
		vec3(-0.819, 0.037, -0.388),
		vec3(0.376, 0.009, 0.193),
		vec3(-0.006, -0.103, -0.035),
		vec3(0.098, 0.393, 0.019),
		vec3(0.542, -0.218, -0.593),
		vec3(0.526, -0.183, 0.424),
		vec3(-0.529, -0.178, 0.684),
		vec3(0.066, -0.657, -0.570),
		vec3(-0.214, 0.288, 0.188),
		vec3(-0.689, -0.222, -0.192),
		vec3(-0.008, -0.212, -0.721),
		vec3(0.053, -0.863, 0.054),
		vec3(0.639, -0.558, 0.289),
		vec3(-0.255, 0.958, 0.099),
		vec3(-0.488, 0.473, -0.381),
		vec3(-0.592, -0.332, 0.137),
		vec3(0.080, 0.756, -0.494),
		vec3(-0.638, 0.319, 0.686),
		vec3(-0.663, 0.230, -0.634),
		vec3(0.235, -0.547, 0.664),
		vec3(0.164, -0.710, 0.086),
		vec3(-0.009, 0.493, -0.038),
		vec3(-0.322, 0.147, -0.105),
		vec3(-0.554, -0.725, 0.289),
		vec3(0.534, 0.157, -0.250),
};

const float fudge_factor_l0 = 2.0;
const float fudge_factor_l1 = 10.0;

const float sh2_weight_l0 = fudge_factor_l0 * 0.28209; 
const vec3 sh2_weight_l1 = vec3(fudge_factor_l1 * 0.48860);
const vec4 sh2_weight = vec4(sh2_weight_l1, sh2_weight_l0) / NUM_KERNLES_SAMPLES;

const float attenuation_angle_threshold = 0.1;

uniform sampler2D depth_texture;
uniform sampler2D noise_texture;
uniform sampler2D gNormalMetal;

uniform float u_radius = 0.6;
uniform float u_bias = 0.025;
uniform float u_strength = 5;
uniform vec2 u_viewportSize;
uniform float u_uvMultiplier = 1.0;

void main()
{
    ivec2 iCoords = ivec2(gl_GlobalInvocationID.xy);

    //Make sure we are not processing more than we need to
    if (any(greaterThanEqual(iCoords, u_viewportSize))) 
    { 
		return;
	}

    vec2 coords = (vec2(iCoords + 0.5) / vec2(u_viewportSize));

    coords *= u_uvMultiplier;

	vec3 ViewPos = depthToViewPos(depth_texture, cam.invProj, coords);
    vec3 WorldPos = (cam.invView * vec4(ViewPos, 1.0)).xyz;
	vec2 NoiseScale = vec2(u_viewportSize.x / 4.0, u_viewportSize.y / 4.0);
	vec3 Noise = texture(noise_texture, coords * NoiseScale).xyz;

	float centerDepth = distance(cam.position.xyz, WorldPos.xyz);	

	vec3 Normal = texture(gNormalMetal, coords).rgb * 2.0 - 1.0;
	
	float Radius = 0.67971 / max(centerDepth, 0.001);
	float MaxDistanceInv = 1.0 / max(1.63942, 0.001);

	vec4 occlusion = vec4(0.0);
	for(int i = 0; i < NUM_KERNLES_SAMPLES; i++)
	{
		vec2 texOffset = reflect(kernelSamples[i].xy, Noise.xy).xy * Radius;
		vec2 sampleCoords = coords + texOffset;
		vec3 samplePosition = depthToViewPos(depth_texture, cam.invProj, sampleCoords);
		vec3 sampleWorldPostion = (cam.invView * vec4(samplePosition, 1.0)).xyz;
		vec3 centerToSample = sampleWorldPostion - WorldPos;
		float dist = length(centerToSample);
		vec3 centerToSampleNormalized = centerToSample / dist;
		
		float attenuation = 1.0 - clamp(dist * MaxDistanceInv, 0.0, 1.0);
		float dp = dot(Normal, centerToSampleNormalized);

		attenuation = attenuation * attenuation * step(attenuation_angle_threshold, dp);

		occlusion += attenuation * sh2_weight * vec4(centerToSampleNormalized, 1.0);
	}

	occlusion = vec4(occlusion * occlusion * occlusion);

	// occlusion = 1.0 - occlusion;

    //occlusion = pow(occlusion, u_strength);

	//occlusion *= 0.5 + 0.5;

    imageStore(ssaoOutput, ivec2(gl_GlobalInvocationID.xy), vec4(occlusion));
}